external inner function string 255 StrReplace(string,string,string);

global
function string 255 FormatJSONString(string tstr)
begin
  string 255 res;
  
  res = StrReplace(tstr,"\\","\\\\");
  res = StrReplace(res,"\"","\\\"");
  res = StrReplace(res,chr(13) & chr(10),"\\n");//chr(13) & 
  res = StrReplace(res,chr(10),"\\n");//chr(13) & 

  FormatJSONString = res;
  return;
end;

global
function string 255 UnFormatJSONString(string tstr)
begin
  string 255 res;
 
  res = StrReplace(tstr,"\\\"","\"");

  UnFormatJSONString = res;
  return;
end;

global
procedure JSONBeginElement(string parameter_name,var area a_req)
begin
  
  if (nonblank(parameter_name)) then begin
    AddTextToArea("\"" & parameter_name & "\":{",a_req);
  end else begin
    AddTextToArea("{",a_req);
  end;
  return;
end;

global
procedure JSONEndElement(Boolean lastf,var area a_req)
begin
  if (lastf) then begin
    AddTextToArea("}",a_req);
  end else begin
    AddTextToArea("},",a_req);
  end;
  return;
end;

global
procedure JSONBeginArray(string parameter_name,var area a_req)
begin
  if (nonblank(parameter_name)) then begin
    AddTextToArea("\"" & parameter_name & "\":[",a_req);
  end else begin
    AddTextToArea("[",a_req);
  end;
  return;
end;

global
procedure JSONEndArray(Boolean lastf,var area a_req)
begin
  if (lastf) then begin
    AddTextToArea("]",a_req);
  end else begin
    AddTextToArea("],",a_req);
  end;
  return;
end;

global
procedure JSONField(string parameter_name,string parameter_value,Boolean quotedf,Boolean lastf,var area a_req)
begin
  if (quotedf) then begin
    AddTextToArea("\"" & parameter_name & "\":\"" & FormatJSONString(parameter_value) & "\"",a_req);
  end else begin
    AddTextToArea("\"" & parameter_name & "\":" & parameter_value,a_req);
  end;
  if (!lastf) then begin
    AddTextToArea(",",a_req);
  end;
  return;
end;

/*
global
procedure JSONUnqualifiedRequestParameter(string parameter_value,Boolean quotedf,Boolean lastf,var area a_req)
begin
  if (quotedf) then begin
    AddTextToArea("\"" & parameter_value & "\"",a_req);
  end else begin
    AddTextToArea(parameter_value,a_req);
  end;
  if (!lastf) then begin
    AddTextToArea(",",a_req);
  end;
  return;
end;
*/

